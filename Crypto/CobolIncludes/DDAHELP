      * COPYBOOK: DDAHELP
      ******************************************************************
      * Areas to help setup parameters for the DKMSEDA API using the   *
      * interface layout as described in DDAV01                        *
      *                                                                *
      * Only needed for function:                                      *
      * DKMSEDAE (C function EDAExtract)                               *
      *                                                                *
      * Use DDAV01 to call the DKMSEDA API                             *
      * Use DDAHELP (this member) to help setup the parameters         *
      * for DDAV01 data area                                           *
      *                                                                *
      * ************************************************************** *
      * This Copy Book can be tuned by the installation to minimize    *
      * memory use.                                                    *
      *                                                                *
      *  Output area:                                                  *
      *  DDA-EXT-RES-DATA                      PIC X(8192).            *
      *  Amount of Static data:                                        *
      *  DDA-EXT-STATIC-DATA                   PIC X(1024).            *
      *  Number of Issuer keys relevant:                               *
      *  OCCURS 1 TO 10 TIMES                                          *
      *  DEPENDING ON DDA-EXT-ISSKEYS-NO.                              *
      *                                                                *
      * ************************************************************** *
      *
      *  Example of calling the API:
      *
      *  WORKING-STORAGE SECTION.
      *  ....
      *  01 STATITC-D1                     PIC X(7) VALUE '*--7--*'.
      *  01 STATITC-D2                     PIC X(5) VALUE '*-5-*'.
      *  COPY DDAV01.
      *  COPY DDAHELP.
      *
      *  PROCEDURE DIVISION.
      *
      *      INITIALIZE DAPI-DDA-V01
      *      SET DDA-EXTRACT-FUNC TO TRUE
      *      SET DDA-DEBUG-LEVEL0 TO TRUE
      *      SET DDA-VERSION-NO-02 TO TRUE
      *
      * * Setup output buffer size
      *      MOVE length of DDA-EXT-RESULT
      *      TO  DDA-EXT-RES-AREA-LGT
      *
      * * Setup parameters for call
      *      MOVE 'POOLTEST' to DDA-EXT-POOL
      *      MOVE 'Y' to DDA-EXT-COMMIT
      *      MOVE 'EMVDDA' to DDA-EXT-KEYFM
      *      MOVE 'JIKKDES1.KEYMNGNT.RECRYPT.EMVDDA.IMP00000'
      *       TO DDA-EXT-KEKOUT
      *      MOVE 2 to DDA-EXT-ISSKEYS-NO
      * * Setup pointers
      *      SET  DDA-EXT-STATIC-PTR   TO ADDRESS OF DDA-EXT-STATIC-ADR
      *      SET  DDA-EXT-ISSUER-PTR   TO ADDRESS OF DDA-EXT-ISSUER-ADR
      *      SET  DDA-EXT-CERTDATA-PTR TO ADDRESS OF
      *           DDA-EXT-CERTDATA-ADR
      *      SET  DDA-EXT-OUTPUT-PTR   TO ADDRESS OF DDA-EXT-RESULT
      * * Fill static data
      *      MOVE 1 to DDA-EXT-STATIC-INDEX
      *
      *      MOVE LENGTH OF STATITC-D1 TO DDA-WORK-BIN-LGT
      *      STRING DDA-WORK-CHR-LGT STATITC-D1 DELIMITED SIZE
      *       INTO DDA-EXT-STATIC-DATA POINTER DDA-EXT-STATIC-INDEX
      *
      *      MOVE LENGTH OF STATITC-D2 TO DDA-WORK-BIN-LGT
      *      STRING DDA-WORK-CHR-LGT STATITC-D2 DELIMITED SIZE
      *       INTO DDA-EXT-STATIC-DATA POINTER DDA-EXT-STATIC-INDEX
      *
      * * Setup Issuer key ids
      *      MOVE 'BEMVMCCA.ISSUER.EMVDDA.00001'
      *       to DDA-EXT-ISSUER-KEYID(1)
      *      MOVE 'BEMVMCCA.ISSUER.EMVDDA.00002'
      *       to DDA-EXT-ISSUER-KEYID(2)
      *
      * * Certificate data
      *      MOVE 'PANDATA1**' TO DDA-EXT-CERT-PAN(1)
      *      MOVE X'0401'      TO DDA-EXT-CERT-DATE(1)
      *      MOVE X'010101'    TO DDA-EXT-CERT-SERIAL(1)
      *      MOVE X'01'        TO DDA-EXT-CERT-HASH-ALG(1)
      *      MOVE X'01'        TO DDA-EXT-CERT-KEY-ALG(1)
      *      MOVE 'PANDATA2**' TO DDA-EXT-CERT-PAN(2)
      *      MOVE X'0402'      TO DDA-EXT-CERT-DATE(2)
      *      MOVE X'020202'    TO DDA-EXT-CERT-SERIAL(2)
      *      MOVE X'01'        TO DDA-EXT-CERT-HASH-ALG(2)
      *      MOVE X'01'        TO DDA-EXT-CERT-KEY-ALG(2)
      *
      *  * Call API
      *      CALL DKMSEDA using DAPI-DDA-V01.
      *
      *  * Display message block
      *       DISPLAY 'DDA-RETURN-TYPE ='      DDA-RETURN-TYPE
      *       DISPLAY 'DDA-RETURN-CODE ='      DDA-RETURN-CODE
      *       DISPLAY 'DDA-REASON-CODE ='      DDA-REASON-CODE
      *       DISPLAY 'DDA-ERROR-LINE ='       DDA-ERROR-LINE
      *       DISPLAY 'DDA-PROGRAM-FILE ='     DDA-PROGRAM-FILE
      *       DISPLAY 'DDA-PROGRAM-MESSAGE ='  DDA-PROGRAM-MESSAGE
      *
      *  * Display function output
      *       DISPLAY 'DDA-EXT-RES-AREA-LGT '  DDA-EXT-RES-AREA-LGT
      *       DISPLAY 'DDA-EXT-RES-USED-LGT ' DDA-EXT-RES-USED-LGT
      *       DISPLAY 'DDA-EXT-RES-ICCDATA-LGT ' DDA-EXT-RES-ICCDATA-LGT
      *       DISPLAY 'DDA-EXT-RES-CERT-COUNT ' DDA-EXT-RES-CERT-COUNT
      *       DISPLAY 'DDA-EXT-RES-DATA ' DDA-EXT-RES-DATA
      *
      *----------------------------------------------------------------*
      * DDA output area                                                *
      *----------------------------------------------------------------*
       01 DDA-EXT-RESULT.
          03 DDA-EXT-RES-AREA-LGT                  PIC 9(09) COMP.
          03 DDA-EXT-RES-USED-LGT                  PIC 9(09) COMP.
          03 DDA-EXT-RES-ICCDATA-LGT               PIC 9(09) COMP.
          03 DDA-EXT-RES-CERT-COUNT                PIC 9(09) COMP.
          03 DDA-EXT-RES-DATA                      PIC X(8192).
      *----------------------------------------------------------------*
      * DDA API pointers and work area                                 *
      *----------------------------------------------------------------*
       01 DDA-EXT-STATIC-INDEX                     PIC 9(09) COMP.
       01 DDA-EXT-STATIC-ADR.
          03 DDA-EXT-STATIC-DATA                   PIC X(1024).
       01 DDA-EXT-WORK1.
          03 DDA-WORK-BIN-LGT            PIC 9(09) COMP.
       01 DDA-EXT-WORK2 REDEFINES DDA-EXT-WORK1.
          03 DDA-WORK-CHR-LGT            PIC X(04).

       01 DDA-EXT-ISSUER-ADR.
          03 DDA-EXT-ISSUER-INDEX
              OCCURS 1 TO 10 TIMES
              DEPENDING ON DDA-EXT-ISSKEYS-NO.
             05 DDA-EXT-ISSUER-KEYID       PIC X(300).

       01 DDA-EXT-CERTDATA-ADR.
          03 DDA-EXT-CERTDATA-INDEX
              OCCURS 1 TO 10 TIMES
              DEPENDING ON  DDA-EXT-ISSKEYS-NO.
             05 DDA-EXT-CERT-PAN           PIC X(10).
             05 DDA-EXT-CERT-DATE          PIC X(02).
             05 DDA-EXT-CERT-SERIAL        PIC X(03).
             05 DDA-EXT-CERT-HASH-ALG      PIC X(01).
             05 DDA-EXT-CERT-KEY-ALG       PIC X(01).
