       CBL PGMNAME(LONGMIXED)
       IDENTIFICATION DIVISION.
       PROGRAM-ID.    "DKMSEDA".
      *****************************************************************
      * COBOL WRAPPER FOR EMV DDA API                                 *
      *****************************************************************
      * CHANGE LOG.                                                   *
      *****************************************************************
      * Version 03.00.00           2021-02-11          Erik Pauner    *
      * THISPROGVERSION updated for new level                         *
      *****************************************************************
      * Version 02.06.00           2018-11-15          Erik Pauner    *
      * THISPROGVERSION updated for new level                         *
      *****************************************************************
      * Version 02.05.00           2017-06-01          Erik Pauner    *
      * THISPROGVERSION updated for new level                         *
      *****************************************************************
      * Version 02.03.00           2016-09-14          Erik Pauner    *
      * THISPROGVERSION updated for new level                         *
      *****************************************************************
      * Version 02.02.00           2016-08-23          Erik Pauner    *
      * THISPROGVERSION updated for new level                         *
      *****************************************************************
      * Version 02.00.00           2014-07-07       Troels Noergaard  *
      * Added entry point DKMSEDAI                                    *
      * THISPROGVERSION updated for new level                         *
      *****************************************************************
      * Version 01.06.00           2010-08-02            Erik Pauner  *
      * THISPROGVERSION updated for new level                         *
      *****************************************************************
      * Version 01.05.00           2010-07-14            Erik Pauner  *
      * THISPROGVERSION updated for new level                         *
      *****************************************************************
      * Version 01.04.00           2009-12-07         Mike Andreasen  *
      * THISPROGVERSION updated for new formats                       *
      *****************************************************************
      * Version 01.03.00           2009-01-30            Erik Pauner  *
      * THISPROGVERSION updated for debug msg                         *
      *****************************************************************
      * Version 01.02.00           2008-10-22            Erik Pauner  *
      * THISPROGVERSION updated for debug msg                         *
      *****************************************************************
      * Version 01.01.00           2008-04-15            Erik Pauner  *
      * THISPROGVERSION updated for debug msg                         *
      *****************************************************************
      * Version 01.00.00           2008-03-27            Erik Pauner  *
      *                                                               *
      * Initial version.                                              *
      *****************************************************************
       ENVIRONMENT DIVISION.
       DATA DIVISION.
       WORKING-STORAGE SECTION.

       01 COPYRIGHT.
          03 FILLER PIC X(154) VALUE
                                  'Licenced Materials - Property of IBM
      -                                                 'IBM - DKMSEDA
      -                            '(C) COPYRIGHT IBM CORP. 2017, 2021
      -                                           'All Rights Reserved.
      -                      'US Government Users Restricted Rights - '.
          03 FILLER PIC X(100) VALUE
                          'Use, duplication or disclosure restricted by
      -                   'GSA ADP Schedule Contract with IBM Corp.   '.

       01 PROGINFO.
          03 THISPROGID                  PIC X(08)  VALUE 'DKMSEDA'.
          03 THISPROGVERSION             PIC X(06)  VALUE '030000'.
          03 THISAUTHOR                  PIC X(10)  VALUE 'EAP/IBM-DK'.
          03 CHANGEDATE                  PIC X(10)  VALUE '2021-02-11'.
       01 WS-DEBUG-MSG           PIC X(18) VALUE '**EDA API DEBUG** '.

       LINKAGE SECTION.
       COPY DDAV01.

       PROCEDURE DIVISION USING DAPI-DDA-V01.

       STEER SECTION.

           IF DDA-DEBUG-LEVEL1
              DISPLAY WS-DEBUG-MSG 'EMV DDA API COBOL WRAPPER ENTERED'
              DISPLAY WS-DEBUG-MSG THISPROGID ' ' THISPROGVERSION
                      ' ' CHANGEDATE
           END-IF

      *    -- Check API input parameters --

           IF NOT DDA-FUNCTION-VALID THEN
             SET DDA-RETURN-API TO TRUE
             STRING DDA-FUNCTION ' is not valid for DDA-FUNCTION'
             DELIMITED SIZE INTO DDA-PROGRAM-MESSAGE
             GO TO EXIT-STEER
           END-IF.

           IF NOT (DDA-VERSION-NO = '02') THEN
             SET DDA-RETURN-API TO TRUE
             STRING DDA-VERSION-NO ' is not valid for DDA-VERSION-NO'
             DELIMITED SIZE INTO DDA-PROGRAM-MESSAGE
             GO TO EXIT-STEER
           END-IF.

      *    -- Do required function --

           EVALUATE TRUE

            WHEN DDA-EXTRACT-FUNC      PERFORM DO-EXTRACT
            WHEN DDA-EXTRACT-ICSF-FUNC PERFORM DO-EXTRACTI
            WHEN DDA-POOLCNT-FUNC      PERFORM DO-POOLCNT

           END-EVALUATE.

       EXIT-STEER.
           GOBACK.
      *****************************************************************
       DO-EXTRACT SECTION.

      * -- Move general debug level and version to extract function
           MOVE DDA-VERSION-NO TO DDA-EXT-VERSION
           MOVE DDA-DEBUG-LEVEL TO DDA-EXT-DEBUG

           IF DDA-DEBUG-LEVEL1
              DISPLAY WS-DEBUG-MSG 'CALLING EDAExtract'
           END-IF

            CALL 'EDAExtract' USING
              BY VALUE ADDRESS OF DDA-RETURN-GROUP
              BY VALUE ADDRESS OF DDA-EXTRACT
              DDA-EXT-OUTPUT-PTR
            END-CALL.

           IF DDA-DEBUG-LEVEL1
              DISPLAY WS-DEBUG-MSG 'RETURN FROM EDAExtract'
           END-IF.

       EXIT-DO-EXTRACT.
           EXIT.
      *****************************************************************
       DO-EXTRACTI SECTION.

      * -- Move general debug level and version to extract function
           MOVE DDA-VERSION-NO TO DDA-EXT-VERSION
           MOVE DDA-DEBUG-LEVEL TO DDA-EXT-DEBUG

           IF DDA-DEBUG-LEVEL1
              DISPLAY WS-DEBUG-MSG 'CALLING EDAExtractI'
           END-IF

            CALL 'EDAExtractI' USING
              BY VALUE ADDRESS OF DDA-RETURN-GROUP
              BY VALUE ADDRESS OF DDA-EXTRACT
              DDA-EXT-OUTPUT-PTR
            END-CALL.

           IF DDA-DEBUG-LEVEL1
              DISPLAY WS-DEBUG-MSG 'RETURN FROM EDAExtractI'
           END-IF.

       EXIT-DO-EXTRACT.
           EXIT.
      *****************************************************************
       DO-POOLCNT SECTION.

           IF DDA-DEBUG-LEVEL1
              DISPLAY WS-DEBUG-MSG 'CALLING EDAPoolCount'
           END-IF

            CALL 'EDAPoolCount' USING
              BY VALUE ADDRESS OF DDA-RETURN-GROUP
              BY REFERENCE DDA-COUNT-POOL
              BY VALUE ADDRESS OF DDA-COUNT-ACTUAL
            END-CALL.

           IF DDA-DEBUG-LEVEL1
              DISPLAY WS-DEBUG-MSG 'RETURN FROM EDAPoolCount'
           END-IF.

       EXIT-DO-EXTRACT.
           EXIT.
      *****************************************************************
       END PROGRAM "DKMSEDA".
