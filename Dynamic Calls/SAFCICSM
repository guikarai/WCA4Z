PROCESS CICS('NOLINKAGE')
       IDENTIFICATION DIVISION.
       PROGRAM-ID.    "SAFCICSM".
      *****************************************************************
      *                                                               *
      * SAFCICSM                                                      *
      *                                                               *
      * Called from API to do QUERY SECURITY - to do SMF audit        *
      *                                                               *
      *****************************************************************
      * Version 02.00.00                                 Erik Pauner  *
      *                                                               *
      * Remove call to get program name                               *
      *                                                               *
      *****************************************************************
      * Version 01.09.00                                 Rene Hyldig  *
      *                                                               *
      * 150707 RH Return of calling program and batch/online-flag in  *
      *           SMF-record.                                         *
      *                                                               *
      *****************************************************************
       ENVIRONMENT DIVISION.
       DATA DIVISION.
       WORKING-STORAGE SECTION.

       01 WS-SAFSMF-PROF-NAME-LGT               PIC 9(08) COMP.
       01 TEST-READ                             PIC 9(08) COMP.
       01 RESP                                  PIC 9(08) COMP.
       01 RESP2                                 PIC 9(08) COMP.
       01 LOCALEIB                              POINTER.
       01 WS-DBUG       PIC X(19)    VALUE '**SAFCICSM DEBUG** '.
       01 DEBUG-STATUS                             PIC 9.
          88 DEBUG-OFF                             VALUE 0.
          88 DEBUG-ON                              VALUE 1.
      * work area to alter input string to SMF
       01 SAFSMF-PROFW.
          05 SAFSMFW-PROF-NAME-LGT          PIC 9(04) COMP.
          05 SAFSMFW-PROF-NAME              PIC X(246).
      * work area to assign invoking program
       01 SAFSMF-PGM-AREA.
          05 SAFSMF-PGM-LGT                   PIC 9(04) COMP.
          05 SAFSMF-PGM                       PIC X(08).
       01 SAFSMF-PGM-READ                     PIC X(08).
      * DUMMY for CICS compiler
       01  dfheiblk.
       02   eibtime  comp-3 pic s9(7).
       02   eibdate  comp-3 pic s9(7).
       02   eibtrnid pic x(4).
       02   eibtaskn comp-3 pic s9(7).
       02   eibtrmid pic x(4).
       02   dfheigdi comp pic s9(4).
       02   eibcposn comp pic s9(4).
       02   eibcalen comp pic s9(4).
       02   eibaid   pic x(1).
       02   eibfn    pic x(2).
       02   eibrcode pic x(6).
       02   eibds    pic x(8).
       02   eibreqid pic x(8).
       02   eibrsrce pic x(8).
       02   eibsync  pic x(1).
       02   eibfree  pic x(1).
       02   eibrecv  pic x(1).
       02   eibfil01 pic x(1).
       02   eibatt   pic x(1).
       02   eibeoc   pic x(1).
       02   eibfmh   pic x(1).
       02   eibcompl pic x(1).
       02   eibsig   pic x(1).
       02   eibconf  pic x(1).
       02   eiberr   pic x(1).
       02   eiberrcd pic x(4).
       02   eibsynrb pic x(1).
       02   eibnodat pic x(1).
       02   eibresp  comp pic s9(8).
       02   eibresp2 comp pic s9(8).
       02   eibrldbk pic x(1).
       01  Wdfhcommarea picture x(1).

       LINKAGE SECTION.
       01 INP-DEBUG-STATUS                       PIC 9.
       01 SAFSMF-RETURN-AREA.
          03 SAFSMF-RETURN-GROUP.
             05 SAFSMF-RETURN-TYPE           PIC S9(08) COMP.
             05 SAFSMF-RETURN-SAF            PIC 9(08) COMP.
             05 SAFSMF-RETURN-RACFRC         PIC 9(08) COMP.
             05 SAFSMF-RETURN-RACFRE         PIC 9(08) COMP.
       01 SAFSMF-DATA-AREA.
          03 SAFSMF-PROF.
             05 SAFSMF-CLASS-LGT              PIC 9(04) COMP.
             05 SAFSMF-CLASS                  PIC X(08).
             05 SAFSMF-PROF-NAME-LGT          PIC 9(04) COMP.
             05 SAFSMF-PROF-NAME              PIC X(246).
       01 INP-CALLING-PGM                     PIC X(08).
      * CICS INTERFACE AREA - for EXEC CICS ADDRESS
       01  Wdfheiblk.
       02   Weibtime  comp-3 pic s9(7).
       02   Weibdate  comp-3 pic s9(7).
       02   Weibtrnid pic x(4).
       02   Weibtaskn comp-3 pic s9(7).
       02   Weibtrmid pic x(4).
       02   Wdfheigdi comp pic s9(4).
       02   Weibcposn comp pic s9(4).
       02   Weibcalen comp pic s9(4).
       02   Weibaid   pic x(1).
       02   Weibfn    pic x(2).
       02   Weibrcode pic x(6).
       02   Weibds    pic x(8).
       02   Weibreqid pic x(8).
       02   Weibrsrce pic x(8).
       02   Weibsync  pic x(1).
       02   Weibfree  pic x(1).
       02   Weibrecv  pic x(1).
       02   Weibfil01 pic x(1).
       02   Weibatt   pic x(1).
       02   Weibeoc   pic x(1).
       02   Weibfmh   pic x(1).
       02   Weibcompl pic x(1).
       02   Weibsig   pic x(1).
       02   Weibconf  pic x(1).
       02   Weiberr   pic x(1).
       02   Weiberrcd pic x(4).
       02   Weibsynrb pic x(1).
       02   Weibnodat pic x(1).
       02   Weibresp  comp pic s9(8).
       02   Weibresp2 comp pic s9(8).
       02   Weibrldbk pic x(1).

       PROCEDURE DIVISION USING INP-DEBUG-STATUS
                                SAFSMF-RETURN-AREA
                                SAFSMF-DATA-AREA
                                INP-CALLING-PGM.
       STEER SECTION.

           MOVE INP-DEBUG-STATUS TO DEBUG-STATUS
           EXEC CICS
             ADDRESS EIB(LOCALEIB)
           END-EXEC.
           SET ADDRESS OF Wdfheiblk TO LOCALEIB

           IF DEBUG-ON
            DISPLAY WS-DBUG 'CALLING PROGRAM = ' INP-CALLING-PGM
           END-IF
           INITIALIZE SAFSMFW-PROF-NAME
           STRING SAFSMF-PROF-NAME(1:SAFSMF-PROF-NAME-LGT)
            '.<ENV>CICS.<PGM>'
            INP-CALLING-PGM
           DELIMITED BY SIZE INTO SAFSMFW-PROF-NAME
      * Move back to input area and recalcualte length
           MOVE SAFSMFW-PROF-NAME TO SAFSMF-PROF-NAME
           PERFORM VARYING SAFSMF-PROF-NAME-LGT FROM 0 BY 1 UNTIL
             SAFSMF-PROF-NAME-LGT = 246 OR
             SAFSMF-PROF-NAME(SAFSMF-PROF-NAME-LGT + 1:1) = SPACE
           END-PERFORM
           IF DEBUG-ON
            DISPLAY WS-DBUG 'RESOURCE LENGTH = ' SAFSMF-PROF-NAME-LGT
           END-IF
      * Get length in format used in EXEC CICS
           MOVE SAFSMF-PROF-NAME-LGT TO
             WS-SAFSMF-PROF-NAME-LGT

           EXEC CICS
             QUERY SECURITY RESCLASS(SAFSMF-CLASS)
             RESIDLENGTH(WS-SAFSMF-PROF-NAME-LGT)
             RESID(SAFSMF-PROF-NAME)
             READ(TEST-READ)
             RESP(RESP)
             RESP2(RESP2)
           END-EXEC.
           IF Weibresp NOT = 0
            MOVE 2 TO SAFSMF-RETURN-TYPE
            MOVE Weibresp TO SAFSMF-RETURN-RACFRC
            MOVE Weibresp2 TO SAFSMF-RETURN-RACFRE
            GO TO EXIT-STEER
           END-IF
      * TEST-READ=35 IF READ ACCESS OK
           IF TEST-READ = 35
            MOVE 0 TO SAFSMF-RETURN-TYPE
           ELSE
            MOVE 3 TO SAFSMF-RETURN-TYPE
            MOVE TEST-READ TO SAFSMF-RETURN-SAF
            MOVE Weibresp TO SAFSMF-RETURN-RACFRC
            MOVE Weibresp2 TO SAFSMF-RETURN-RACFRE
           END-IF
           .
       EXIT-STEER.
           GOBACK.

       END PROGRAM "SAFCICSM".
